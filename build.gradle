plugins {
	id 'java'
	id 'maven-publish'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'nebula.release' version '21.0.0'
}

group = 'io.boot2prod'
description = 'Demo project to deploy to environments'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

bootJar {
  archiveClassifier = 'spring'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register("printVersion") {
  doLast { println(project.version) }
}

spotless {
	java {
		palantirJavaFormat()
		importOrder()
		removeUnusedImports()
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact tasks.named('bootJar')
		}
	}
	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri("https://maven.pkg.github.com/DanielWillian/spring-app-deploy")
			credentials {
				username = System.getenv('GITHUB_ACTOR')
				password = System.getenv('GITHUB_TOKEN')
			}
		}
	}
}
